buildscript {
    ext {
        springBootVersion = '3.5.8'
		mockitoVersion = '5.20.0'
    }
    repositories {
        mavenCentral()
		gradlePluginPortal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:7.1.0.6387")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

sonar {
	properties {
		property "sonar.projectKey", "conorheffron_ironoc-db"
		property "sonar.organization", "conorheffron"
		property "sonar.java.coveragePlugin", "jacoco"
		property "sonar.sources", ["src/test", "src/main/java"]
		property "sonar.tests", ["src/test", "src/integration-test"]
		property "sonar.test.inclusions", "**/*Test.java"
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.sources", "src/main/java"
		property "sonar.java.coveragePlugin", "jacoco"
	}
}

group = 'conorheffron'
version = '7.0.12'
description = "iRonoc-DB Sample Data Manager"

java {
	sourceCompatibility = JavaVersion.VERSION_25
	targetCompatibility = JavaVersion.VERSION_25
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    integrationTestImplementation.extendsFrom testImplementation
}

dependencies {
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    // Spring
    implementation 'org.springframework.boot:spring-boot-maven-plugin:' + springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-web:' + springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:' + springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:' + springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-tomcat:' + springBootVersion
    implementation 'org.springframework.boot:spring-boot-starter-validation:' + springBootVersion

    // GC Secret Manager
    implementation 'com.google.cloud:google-cloud-secretmanager:2.80.0'

    // SQL
    implementation 'mysql:mysql-connector-java:8.0.33'
    implementation 'com.h2database:h2:2.4.240'

    implementation 'org.apache.maven.reporting:maven-reporting-api:4.0.0'
	implementation 'org.jacoco:jacoco-maven-plugin:0.8.14'
    implementation 'org.apache.tomcat.embed:tomcat-embed-jasper:11.0.14'
    implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation('org.springframework.boot:spring-boot-starter-test:' + springBootVersion) {
		exclude(module: 'commons-logging')
		exclude(module: 'mockito-core')
    }

	testImplementation 'org.mockito:mockito-core:' + mockitoVersion
	testImplementation 'org.mockito:mockito-junit-jupiter:' + mockitoVersion
	testImplementation 'org.mockito:mockito-inline:5.2.0'
	testImplementation 'net.bytebuddy:byte-buddy:1.18.1'
}

test {
	jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }.absolutePath}"
    testLogging.showStandardStreams = true
    def testCount = 0
    afterTest { descriptor, result ->
        logger.lifecycle("Test {count: ${++testCount} name: $descriptor.name result: $result.resultType}")
    }
}

tasks.register('integrationTest', Test) {
    jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }.absolutePath}"
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    finalizedBy jacocoTestReport
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

jacocoTestReport {
    reports {
        xml.required = true
    }
}

integrationTest {
    testLogging.showStandardStreams = true
    def testCount = 0
    afterTest { descriptor, result ->
        logger.lifecycle("Test {count: ${++testCount} name: $descriptor.name result: $result.resultType}")
    }
}

// Build executable war file
tasks.war {
    enabled = true
    // Remove `plain` postfix from war file name
    archiveClassifier.set("")
}

publishing {
    repositories {
        maven {
            name = "Github"
            url = uri("https://maven.pkg.github.com/conorheffron/ironoc-db")
            credentials {
                username = System.getenv("USERNAME")
                password = System.getenv("TOKEN")
            }
        }
    }
    publications {
        mavenWeb(MavenPublication) {
		    from components.web
	    }
    }
}

springBoot {
    buildInfo()
}

tasks.withType(JavaExec).configureEach {
    jvmArgs += "--enable-native-access=ALL-UNNAMED"
    jvmArgs += "--sun-misc-unsafe-memory-access=allow"
}
